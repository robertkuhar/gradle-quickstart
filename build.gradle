import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'jetty'

version = '1.0'

ext.env = new HashMap(System.getenv())
ext.env.putAll(System.properties)
ext.buildTag = System.env.BUILD_TAG ?: "dev"
ext.buildNum = System.env.BUILD_NUMBER ?: "dev"

ext.jetty = [version: '9.2.5.v20141112']
ext.jersey = [version: '2.13']

defaultTasks 'clean', 'build'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.7'
  compile 'ch.qos.logback:logback-classic:1.1.2'
  compile "org.glassfish.jersey.core:jersey-server:$jersey.version"
  compile "org.glassfish.jersey.containers:jersey-container-servlet:$jersey.version"
  compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey.version"
  compile 'com.google.guava:guava:18.0'
  compile 'org.apache.httpcomponents:httpclient:4.3.6'
       
  providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
  providedCompile 'javax.ws.rs:javax.ws.rs-api:2.0'
    
  testCompile 'org.testng:testng:6.8.8'
  testCompile "org.glassfish.jersey.test-framework:jersey-test-framework-core:$jersey.version"
  testCompile "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-jetty:$jersey.version"
  testCompile "org.eclipse.jetty:jetty-server:$jetty.version"
  testCompile "org.eclipse.jetty:jetty-webapp:$jetty.version"
}

test {
    useTestNG()
}

task integrationTest(type: Test, dependsOn: 'test') {
    useTestNG() {
      includeGroups 'integration'
    }
}

processResources {
    def whoami = System.getProperty( 'user.name' );
    def hostname = InetAddress.getLocalHost().getHostName()
    def buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss z')
    
    filter ReplaceTokens, tokens: [
                "buildsig.version" : project.version,
                "buildsig.timestamp" : buildTimestamp,
                "buildsig.user" : whoami,
                "buildsig.system" : hostname,
                "buildsig.tag" : buildTag
            ]
}

war {
    manifest {
        attributes("Implementation-Title": "gradle-quickstart", "Implementation-Version": version)
    }
}
